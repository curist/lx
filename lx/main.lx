let helpCmd = import "commands/help.lx"
let versionCmd = import "commands/version.lx"
let compileCmd = import "commands/compile.lx"
let runCmd = import "commands/run.lx"
let evalCmd = import "commands/eval.lx"
let replCmd = import "commands/repl.lx"
let disasmCmd = import "commands/disasm.lx"
let astCmd = import "commands/ast.lx"
let checkCmd = import "commands/check.lx"
let lspCmd = import "commands/lsp.lx"

fn isHelpArg(arg) {
  arg == "-h" or arg == "--help" or arg == "help"
}

let commands = [runCmd, evalCmd, replCmd, compileCmd, disasmCmd, astCmd, checkCmd, lspCmd, versionCmd, helpCmd]
let commandsByName = .{}
for let i = 0; i < len(commands); i = i + 1 {
  let c = commands[i]
  commandsByName[c.name] = c
  let aliases = c.aliases or []
  for let j = 0; j < len(aliases); j = j + 1 {
    commandsByName[aliases[j]] = c
  }
}

fn handleUnknown(ctx, cmdName) {
  if cmdName {
    Lx.stderr.println(ctx.prog + " " + cmdName + ": unknown command")
  }
  Lx.stderr.println("Run '" + ctx.prog + " help' for usage.")
  Lx.exit(28)
}

let prog = Lx.args[0]
let cmdName = Lx.args[1] or "help"

// Support `lx <cmd> --help` as sugar for `lx help <cmd>`.
let helpTarget = nil
if cmdName != "help" and isHelpArg(Lx.args[2]) {
  helpTarget = cmdName
  cmdName = "help"
}

let ctx = .{
  argv: helpTarget and [prog, "help", helpTarget] or Lx.args,
  prog,
  commands,
  commandsByName,
}

if len(ctx.argv) < 2 {
  helpCmd.run(ctx)
  Lx.exit(28)
}

let cmd = ctx.commandsByName[cmdName]
{cmd and cmd.run or fn() { handleUnknown(ctx, cmdName) }}(ctx)
