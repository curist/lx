let suite = (import "test/makeTestSuite.lx")()
let test = suite.defineTest

test("concat", fn(assert) {
  let arr1 = [1]
  let arr2 = [2]
  assert.equal(len(arr1), 1)
  // concat shouldn't affect original arr
  concat(arr1, arr2)
  assert.equal(len(arr1), 1)
  arr1 = concat(arr1, arr2)
  assert.equal(len(arr1), 2)

  arr2 = []
  arr1 = concat(arr1, arr2)
  assert.equal(len(arr1), 2)
})

test("unicode", fn(assert) {
  assert.equal(len("你好世界"), 12)
  assert.equal(len("やれやれだぜ"), 18)
  assert.equal(len(range("やれやれだぜ")), 6)
})

test("->", fn(assert) {
  fn add(a, b) { a + b }
  fn mul(a, b) { a * b }
  let result = 2->{fn(x) { x->add(3) }}()->mul(4)
  assert.equal(result, 20)
})

test("keyword as hashmap key", fn(assert) {
  let x = .{ and: 1, if: 2 }
  assert.equal(x.and, 1)
  assert.equal(x.if, 2)
})

test("else expression", fn(assert) {
  assert.equal(if false {0} else {1}, 1)
  assert.equal((if true {0} else {1} + 10) + 10, 20)
})

test("lines", fn(assert) {
  let splitted = "a\nb\nc"->lines()
  assert.equal(len(splitted), 3)
  assert.equal(splitted[0], "a")
  assert.equal(splitted[1], "b")
  assert.equal(splitted[2], "c")
})

test("join preserves NUL", fn(assert) {
  let s = join([chr(0), chr(1), "a"], "")
  assert.equal(len(s), 3)

  assert.equal(s[0], chr(0))
  assert.equal(s[1], chr(1))
  assert.equal(s[2], "a")

  assert.equal(len(str(chr(0))), 1)
})

test("nameOf", fn(assert) {
  let Color = enum { Red, Green = 3, Blue }
  assert.equal(nameOf(Color, Color.Red), "Red")
  assert.equal(Color->nameOf(Color.Blue), "Blue")
  assert.equal(nameOf(Color, 999), nil)
})

suite.run()
