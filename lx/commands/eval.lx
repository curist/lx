let common = import "commands/common.lx"

fn run(ctx) {
  let src = nil
  if len(ctx.argv) >= 3 {
    src = ctx.argv[2]
  } else if Lx and Lx.stdin and Lx.stdin.readAll {
    src = Lx.stdin.readAll()
  }

  if !src or src == "" {
    Lx.stderr.println("Usage: " + ctx.prog + " eval <expression>")
    Lx.stderr.println("  or pipe input via stdin: echo 'println(42)' | " + ctx.prog + " eval")
    Lx.exit(64)
  }

  let path = "[LX EVAL]"
  // Use a synthetic entry path rooted at CWD so `.lxroot` discovery and imports
  // behave like running a normal entry file from the current directory.
  let entryPath = Lx.path.join(Lx.fs.cwd(), "eval.lx")
  let d = common.makeDriverWithSource(src, path, entryPath)
  let func = common.compileWithDriver(d, path)
  if !func { Lx.exit(65) }

  let bytes = common.objbuilder(func, false).bytes()
  let program = Lx.loadObj(bytes, false)
  let result = program()
  println(result)
}

.{
  name: "eval",
  aliases: [],
  usage: "eval <expression>",
  summary: "Evaluate expression (arg or stdin)",
  run: run,
}
