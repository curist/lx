fn makeTestSuite() {
  let tests = []
  let totalAssertions = 0
  let passedAssertions = 0

  fn defineTest(testName, testBody) {
    let testPassed = true
    let assert = .{
      equal: fn(actual, expect) {
        totalAssertions = totalAssertions + 1
        if expect == actual {
          passedAssertions = passedAssertions + 1
          return
        }
        print(
          "assertion fail:", testName, "\n",
          "\texpect:", expect, "\n",
          "\tactual:", actual, "\n",
        )
        testPassed = false
      },
    }
    push(tests, fn() {
      testBody(assert)
      testPassed
    })
  }

  fn run() {
    let passedTests = range(tests)->fold(0, fn(acc, testCase) {
      acc + { testCase() and 1 or 0 }
    })
    print(
      "passed tests", str(passedTests) + "/" + len(tests)->str(),
      "asserts", str(passedAssertions) + "/" + str(totalAssertions),
    )

    Lx.exit(len(tests) == passedTests and 0 or 1)
  }

  return .{
    defineTest: defineTest,
    run: run,
  }
}
